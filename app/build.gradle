import com.sokhibdzhon.livedota.buildsrc.Configs
import com.sokhibdzhon.livedota.buildsrc.Deps
import com.sokhibdzhon.livedota.buildsrc.Plugins

apply plugin: Plugins.androidApplication

apply plugin: Plugins.kotlinAndroid
apply plugin: Plugins.kotlinKapt
apply plugin: Plugins.kotlinAndroidExtensions
apply plugin: Plugins.kotlinNavigationSafeArgs
apply plugin: Plugins.gmsGoogleServices
apply plugin: Plugins.firebaseCrashlytics

android {
    compileSdkVersion Configs.compileSdkVersion
    buildToolsVersion Configs.buildToolsVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
                "-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"]
    }
    defaultConfig {
        applicationId Configs.applicationId
        minSdkVersion Configs.minSdkVersion
        targetSdkVersion Configs.targetSdkVersion
        versionCode Configs.versionCode
        vectorDrawables.useSupportLibrary = true
        versionName Configs.versionName
        testInstrumentationRunner Configs.testInstrumentationRunner
    }
    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"https://api.opendota.com/api/\""
            buildConfigField "String", "STEAM_URL", "\"https://api.steampowered.com/\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "String", "SERVER_URL", "\"https://api.opendota.com/api/\""
            buildConfigField "String", "STEAM_URL", "\"https://api.steampowered.com/\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Deps.kotlinStdlib
    implementation Deps.appcompat
    implementation Deps.ktxCore
    implementation Deps.constraintLayout
    implementation Deps.legacySupport
    implementation Deps.lifecycleExtension
    implementation Deps.lifecycleViewModel
    implementation Deps.lifecycleLiveData
    implementation Deps.lifecycleRuntime
    implementation Deps.lifecycleRuntimeKtx
    testImplementation Deps.junit
    implementation Deps.material
    androidTestImplementation Deps.testJUnit
    androidTestImplementation Deps.espressoCore

    //Navigation
    implementation Deps.navigationKtx
    implementation Deps.navigationUiKtx

    //Retrofit
    implementation Deps.retrofit
    implementation Deps.retrofitGsonConverter
    implementation Deps.loggingInterceptor

    //Dagger
    implementation Deps.dagger
    kapt Deps.daggerCompiler

    //Coroutine
    implementation Deps.coroutine

    // Room
    implementation Deps.roomRuntime
    kapt Deps.roomCompiler
    implementation Deps.roomKtx

    //Firebase
    implementation Deps.firebase
    implementation Deps.crashlytics

    // Gson
    implementation Deps.gson

    //Timber
    implementation Deps.timber

    //Glide
    implementation Deps.glide
    kapt Deps.glideCompiler
    //DB
    debugImplementation Deps.debugDb
}
